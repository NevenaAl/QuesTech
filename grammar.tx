Assessment:
    'title' ':' title=STRING
    ('description' ':' description=STRING)?
    ('ask_for_personal_info' ':' ask_for_personal_info=BOOL)?
    assessment_details=AssessmentType
;

AssessmentType:
    Quiz | Survey | ScoredSurvey | Poll
;

Quiz:
    'type' ':' type='Quiz' 
    'questions' ':' '[' questions+=QuizQuestion[/;|,|\s*/] ']'
    (end=AssessmentEnd?
    ('default_points_per_question' ':' default_points_per_question=INT)?
    ('default_negative_points_per_question' ':' default_negative_points_per_question=INT)?
    (pass_criteria=QuizPassCriteria)?)#
;

Survey:
    'type' ':' type='Survey'
    'questions' ':' '[' questions+=SurveyQuestion[/;|,|\s*/] ']'
    (end=AssessmentEnd)? 
;

ScoredSurvey:
    'type' ':' type='Scored Survey'
    'questions' ':' '[' questions+=ScoredSurveyQuestion[/;|,|\s*/] ']'
    ((end=AssessmentEnd)? 
    'scoring' ':' scoring+=SurveyScoring)#
;

Poll:
    'type' ':' type='Poll'
    'question' ':' question=PollQuestion
;

SurveyScoring:
    ('range_start' ':' range_start=INT
    'range_end' ':' range_end=INT
    'result' ':' result=STRING | INT)#
;

PollQuestion:
    ('text' ':' text=STRING
    question_details=PollQuestionType)#
;

SurveyQuestion:
    '{' ('text' ':' text=STRING
    ('required' ':' required=BOOL)?
    question_details=SurveyQuestionType)# '}'
;

QuizQuestion:
    '{' ('text' ':' text=STRING
    question_details=QuizQuestionType
    (questionPoints=QuestionPoints)?)# '}'  //ask: check if there is no points here and quizpasscore has poinstrequired 
;

ScoredSurveyQuestion:
    '{' ('text' ':' text=STRING
    question_details=SingleChoiceQuestion)# '}'
;

SurveyQuestionType:
    OpenEndedQuestion | SingleChoiceQuestion | MultipleChoiceQuestion | TrueFalseQuestion | NumberQuestion | OpinionScaleQuestion | RatingQuestion
;

QuizQuestionType:
    OpenEndedQuestion | SingleChoiceQuestion | MultipleChoiceQuestion | TrueFalseQuestion | NumberQuestion
;

PollQuestionType:
    SingleChoiceQuestion | TrueFalseQuestion | RatingQuestion | OpinionScaleQuestion
;

QuizPassCriteria:
    ('percentage_required' ':' percentage_required=INT) |
    ('number_of_correct_answers_required' ':' num_of_correct_answers_required=INT) |
    ('points_required' ':' points_required=INT) //ask da li u gramatici nekako ograniciti da ne navede ovde procenat a dodeli poene pitanjima
;

QuestionPoints:
    'points' ':' positive=INT
    ('negative_points' ':' negative=INT)?
;

OpenEndedQuestion:
    'type' ':' type='OpenEnded'
;

TrueFalseQuestion:
    ('type' ':' type='TrueFalse'
    ('correct_answer' ':' correct_answer=BOOL)?
    ('subtype' ':' subtype=TrueFalseSubtype)?)#
;

TrueFalseSubtype:
    'yes/no' | 'true/false'
;

SingleChoiceQuestion:
    ('type' ':' type='SingleChoice'
    'answers' ':' '[' answers+=Answer[/;|,|\s*/] ']')#
    //ask: should we check if there is only one correct answer here
;

MultipleChoiceQuestion:
    ('type' ':' type='MultipleChoice'
    'answers' ':' '[' answers+=Answer[/;|,|\s*/] ']'
    'accept_partial_answer' ':' accept_partial_answer=BOOL?)# //if question has 5 points, give 2.5 for 1/2 correct 
;

NumberQuestion:
    ('type' ':' type='Number'
    'answer' ':' answer=INT)# 
;

OpinionScaleQuestion:
    ('type' ':' type='OpinionScale'
    scale=ScaleType)#
;

ScaleType:
    RangeScale | CustomScale
;

RangeScale:
    ('scale_start' ':' scale_start=INT
    'scale_end' ':' scale_end=INT
    ('scale_start_label' ':' scale_start_label=STRING)?
    ('scale_end_label' ':' scale_end_label=STRING)?)#
;

CustomScale:
    'scale_options' ':' '[' scale_options+=ScaleOption[/;|,|\s*/] ']'
;

ScaleOption:
    '{' ('value' ':' value=INT
    'label' ':' label=STRING)# '}'
;

RatingQuestion:
    ('type' ':' type='Rating'
    'max_rate' ':' max_rate=INT)#
;

Answer:
    '{' ('text' ':' text=STRING
    ('is_correct' ':' is_correct=BOOL | 'points' ':' points=INT)? )# '}'
    //there is no correct answer in survey and poll
    //scored survey has points for every answer
    //ask: da li razdvojiti answers
;

AssessmentEnd:
    (('completion_time' ':' completion_time+=Time)?
    ('can_skip_to_end' ':' can_skip_to_end=BOOL)?)#
;

Time:
    time=INT unit=TimeUnit
;

TimeUnit:
    's' | 'm' | 'h'
;

//dodati date, ranking pitanje, uslovna pitanja?