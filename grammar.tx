Assessment:
    'title' ':' title=STRING
    ('description' ':' description=STRING)?
    (ask_for_personal_info?="ask_for_personal_info"
    assessment_details=AssessmentType)#
;

AssessmentType:
    Quiz | Survey | ScoredSurvey | Poll
;

Quiz:
    'type' ':' type='quiz' 
    'questions' ':' '[' questions+=QuizQuestion[/;|,|\s*/] ']'
    ((end=AssessmentEnd)?
    ('default_points_per_question' ':' default_points_per_question=FLOAT)?
    ('default_negative_points_per_question' ':' default_negative_points_per_question=FLOAT)?
    (pass_criteria=QuizPassCriteria)?)#
;

Survey:
    'type' ':' type='survey'
    'questions' ':' '[' questions+=SurveyQuestion[/;|,|\s*/] ']'
    (end=AssessmentEnd)? 
;

ScoredSurvey:
    'type' ':' type='scored_survey'
    'questions' ':' '[' questions+=ScoredSurveyQuestion[/;|,|\s*/] ']'
    ((end=AssessmentEnd)? 
    'scoring' ':' '[' scoring+=SurveyScoring[/;|,|\s*/] ']')#
;

Poll:
    'type' ':' type='poll'
    'question' ':' question=PollQuestion
;

SurveyScoring:
    '{' ('range_start' ':' range_start=INT
    'range_end' ':' range_end=INT
    'result' ':' result=ScoredSurveyResultType)# '}'
;

ScoredSurveyResultType:
    STRING | FLOAT
;

PollQuestion:
    ('text' ':' text=STRING
    question_details=PollQuestionType)#
;

SurveyQuestion:
    '{' ('text' ':' text=STRING
    (required?='required')?
    question_details=SurveyQuestionType)# '}'
;

QuizQuestion:
    '{' ('text' ':' text=STRING
    question_details=QuizQuestionType
    (question_points=QuestionPoints)?)# '}'  
;

ScoredSurveyQuestion:
    '{' ('text' ':' text=STRING
    (required?='required')?
    question_details=SingleChoiceQuestion)# '}'
;

SurveyQuestionType:
    OpenEndedQuestion | SingleChoiceQuestion | MultipleChoiceQuestion | TrueFalseQuestion | NumberQuestion | OpinionScaleQuestion | RatingQuestion
;

QuizQuestionType:
    OpenEndedQuestion | SingleChoiceQuestion | MultipleChoiceQuestion | TrueFalseQuestion | NumberQuestion
;

PollQuestionType:
    SingleChoiceQuestion | TrueFalseQuestion | RatingQuestion | OpinionScaleQuestion
;

QuizPassCriteria:
    ('percentage_required' ':' percentage_required=FLOAT) |
    ('number_of_correct_answers_required' ':' num_of_correct_answers_required=INT) |
    ('points_required' ':' points_required=FLOAT) 
;

QuestionPoints:
    'points' ':' positive=FLOAT
    ('negative_points' ':' negative=FLOAT)?
;

OpenEndedQuestion:
    'type' ':' type='open_ended'
;

TrueFalseQuestion:
    ('type' ':' type='true_false'
    ('correct_answer' ':' correct_answer=BOOL)? 
    ('subtype' ':' subtype=TrueFalseSubtype)?)#
;

TrueFalseSubtype:
    'yes/no' | 'true/false'
;

SingleChoiceQuestion:
    ('type' ':' type='single_choice'
    'answers' ':' '[' answers+=Answer[/;|,|\s*/] ']')#
;

MultipleChoiceQuestion:
    ('type' ':' type='multiple_choice'
    'answers' ':' '[' answers+=Answer[/;|,|\s*/] ']'
    accept_partial_answer?='accept_partial_answer')# 
;

NumberQuestion:
    ('type' ':' type='number'
    ('answer' ':' answer=FLOAT)?)# 
;

OpinionScaleQuestion:
    ('type' ':' type='opinion_scale'
    scale=ScaleType)#
;

ScaleType:
    RangeScale | CustomScale
;

RangeScale:
    ('scale_start' ':' scale_start=INT
    'scale_end' ':' scale_end=INT
    ('scale_start_label' ':' scale_start_label=STRING)?
    ('scale_end_label' ':' scale_end_label=STRING)?)#
;

CustomScale:
    'scale_options' ':' '[' scale_options+=ScaleOption[/;|,|\s*/] ']'
;

ScaleOption:
    '{' ('value' ':' value=INT
    'label' ':' label=STRING)# '}'
;

RatingQuestion:
    ('type' ':' type='rating'
    'max_rate' ':' max_rate=INT)#
;

Answer:
    '{' ('text' ':' text=STRING
    (is_correct="is_correct" | 'points' ':' points=FLOAT)? )# '}'
;

AssessmentEnd:
    (('completion_time' ':' /\s*/ completion_time=Time)?
    can_skip_to_end?="can_skip_to_end")#
;

TimeHours[noskipws]:
    hours=INT 'h'
;

TimeMinutes[noskipws]:
    minutes=INT 'm'
;

TimeSeconds[noskipws]:
    seconds=INT 's'
;

Time:
   (TimeHours TimeMinutes TimeSeconds) | (TimeHours TimeMinutes) | (TimeHours TimeSeconds) | (TimeMinutes TimeSeconds) | TimeHours | TimeMinutes | TimeSeconds
;
