Assessment:
    'title' ':' title=STRING
    ('description' ':' description=STRING)?
    (ask_for_personal_info?="ask_for_personal_info"
    assessment_details=AssessmentType)#
;

AssessmentType:
    Quiz | Survey | ScoredSurvey | Poll
;

Quiz:
    'type' ':' type='quiz' 
    'questions' ':' questions+=QuizQuestion[/\s*/] 
    ((end=AssessmentEnd)?
    ('default_points_per_question' ':' default_points_per_question=FLOAT)?
    ('default_negative_points_per_question' ':' default_negative_points_per_question=FLOAT)?
    (pass_criteria=QuizPassCriteria)?)#
;

Survey:
    'type' ':' type='survey'
    'questions' ':' questions+=SurveyQuestion[/\s*/] 
;

ScoredSurvey:
    ('type' ':' type='scored_survey'
    'questions' ':' questions+=ScoredSurveyQuestion[/\s*/] 
    'scoring' ':' scoring+=SurveyScoring[/\s*/])#
;

Poll:
    'type' ':' type='poll'
    'question' ':' question=PollQuestion
;

SurveyScoring:
    '-' range_start=INT '-' range_end=INT ':' result=ScoredSurveyResultType
;

ScoredSurveyResultType:
    STRING | FLOAT
;

PollQuestion:
    ('text' ':' text=STRING
    question_details=PollQuestionType)#
;

SurveyQuestion:
    'Q' ':' ('text' ':' text=STRING
    (required?='required')?
    question_details=SurveyQuestionType)# 
;

QuizQuestion:
    'Q' ':' ('text' ':' text=STRING
    question_details=QuizQuestionType
    (question_points=QuestionPoints)?)#
;

ScoredSurveyQuestion:
    'Q' ':' ('text' ':' text=STRING
    (required?='required')?
    question_details=SingleChoiceQuestion)# 
;

SurveyQuestionType:
    OpenEndedQuestion | SingleChoiceQuestion | MultipleChoiceQuestion | TrueFalseQuestion | NumberQuestion | OpinionScaleQuestion | RatingQuestion
;

QuizQuestionType:
    OpenEndedQuestion | SingleChoiceQuestion | MultipleChoiceQuestion | TrueFalseQuestion | NumberQuestion
;

PollQuestionType:
    SingleChoiceQuestion | TrueFalseQuestion | RatingQuestion | OpinionScaleQuestion
;

QuizPassCriteria:
    ('percentage_required' ':' percentage_required=FLOAT) |
    ('number_of_correct_answers_required' ':' num_of_correct_answers_required=INT) |
    ('points_required' ':' points_required=FLOAT) 
;

QuestionPoints:
    'points' ':' positive=FLOAT
    ('negative_points' ':' negative=FLOAT)?
;

OpenEndedQuestion:
    'type' ':' type='open_ended'
;

TrueFalseQuestion:
    ('type' ':' type='true_false'
    ('correct_answer' ':' correct_answer=TrueFalseAnswer)? 
    ('subtype' ':' subtype=TrueFalseSubtype)?)#
;

TrueFalseAnswer:
    'yes' | 'no' | 'true' | 'false'
;

TrueFalseSubtype:
    'yes/no' | 'true/false'
;

SingleChoiceQuestion:
    ('type' ':' type='single_choice'
    'answers' ':' answers+=Answer[/\s*/])#
;

MultipleChoiceQuestion:
    ('type' ':' type='multiple_choice'
    'answers' ':' answers+=Answer[/\s*/]
    accept_partial_answer?='accept_partial_answer')# 
;

NumberQuestion:
    ('type' ':' type='number'
    ('correct_answer' ':' correct_answer=FLOAT)?)# 
;

OpinionScaleQuestion:
    ('type' ':' type='opinion_scale'
    scale=ScaleType)#
;

ScaleType:
    RangeScale | CustomScale
;

RangeScale:
    ('scale' ':' scale_start=INT '-' scale_end=INT
    ('scale_start_label' ':' scale_start_label=STRING)?
    ('scale_end_label' ':' scale_end_label=STRING)?)#
;

CustomScale:
    'scale_options' ':' scale_options+=ScaleOption[/\s*/]
;

ScaleOption:
    '-' '[' value=INT ']' label=STRING
;

RatingQuestion:
    ('type' ':' type='rating'
    'max_rate' ':' max_rate=INT)#
;

Answer:
    '-' ('[]' | ('[' (is_correct?='x' | points=FLOAT) ']'))?  text=STRING
;

AssessmentEnd:
    (('completion_time' ':' /\s*/ completion_time=Time)?
    can_skip_to_end?="can_skip_to_end")#
;

TimeHours[noskipws]:
    amount=MyInt 'h'
;

TimeMinutes[noskipws]:
    amount=MyInt 'm'
;

TimeSeconds[noskipws]:
    amount=MyInt 's'
;

MyInt:
    /[0-9]+/
;

Time:
   (hours=TimeHours ':'? (minutes=TimeMinutes)? ':'? (seconds=TimeSeconds)?) | (minutes=TimeMinutes ':'? (seconds=TimeSeconds)?) | seconds=TimeSeconds
;

